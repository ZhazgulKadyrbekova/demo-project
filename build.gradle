plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '2.7.12'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.flywaydb.flyway' version '9.8.1'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
	id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

group = 'crud.mvc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

querydsl {
	library = 'com.querydsl:querydsl-apt:5.0.0'
	jpa = true
}

configurations {
	querydsl.extendsFrom implementation, compileClasspath
}

compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}

project.afterEvaluate {
	project.tasks.compileQuerydsl.options.compilerArgs = [
			"-proc:only",
			"-processor", project.querydsl.processors() +
					',lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
	]
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', 'src/querydsl/java']
		}
	}
}

dependencies {
	implementation 'org.springdoc:springdoc-openapi-core:1.1.49'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity5'
	implementation 'org.flywaydb:flyway-core'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'

	implementation 'com.querydsl:querydsl-jpa:5.0.0'

	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'

	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}